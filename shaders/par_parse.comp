#version 450
layout(local_size_x = 1) in;    

const uint PREC_GIVES = 1;
const uint PREC_EQUALS = 2;
const uint PREC_TAKES = 3;
const uint PREC_UNDEF = 0;

struct StackSym {
    uint sym;
    uint prec;
};

struct StackInfo {
    uint stack_base;
    uint top_term;
    uint gives_nr;
};

struct ParseError {
    uint error;
    uint location;
};

layout(set = 0, binding = 0) buffer AlphaBuffer {
    uint[] alpha;
};

layout(set = 0, binding = 1) buffer HeadsBuffer {
    uint[] heads;
};

layout(set = 0, binding = 2) buffer EndsBuffer {
    uint[] ends;
};

layout(set = 0, binding = 3) buffer StackBuffer {
    StackSym[] stack;
};

layout(set = 0, binding = 4) buffer StackBaseBuffer {
    uint[] stack_base;
};

layout(set = 0, binding = 5) buffer StackPtrBuffer {
    uint[] stack_ptr;
};

layout(set = 0, binding = 6) buffer GivesStackBuffer {
    uint[] gives_stack;
};

layout(set = 0, binding = 7) buffer BotGivesBuffer {
    uint[] bot_gives;
};

layout(set = 0, binding = 8) buffer PrecMatBuffer {
    uint[] prec_mat;
};

layout(set = 0, binding = 9) buffer RulesBuffer {
    uint[] rules;
};

layout(set = 0, binding = 10) buffer TermThreshBuffer {
    uint term_thresh;
};

layout(set = 0, binding = 11) buffer ErrorBuffer {
    ParseError[] error;
};

bool is_nt(uint sym) {
    return sym >= term_thresh;
}

StackInfo stack_init() {
    uint stack_base_ = stack_base[gl_GlobalInvocationID.x];
    uint top_term = 0;
    uint gives_nr = 0;

    for (uint i = stack_base_; i < stack_ptr[gl_GlobalInvocationID.x]; i++) {
        if (stack[i].prec == PREC_GIVES) {
            gives_stack[stack_base_ + gives_nr] = i;
            gives_nr++;
        }
        if (!is_nt(stack[i].sym)) {
            top_term = i;
        }
    }
    
    return StackInfo(
        stack_base_,
        top_term,
        gives_nr
    );
}

void stack_push(inout StackInfo self, StackSym sym) {
    stack[stack_ptr[gl_GlobalInvocationID.x]] = sym;
    if (!is_nt(sym.sym)) {
        self.top_term = stack_ptr[gl_GlobalInvocationID.x];
    }
    if (sym.prec == PREC_GIVES) {
        gives_stack[self.stack_base + self.gives_nr] = stack_ptr[gl_GlobalInvocationID.x];
        self.gives_nr++;
    }
    stack_ptr[gl_GlobalInvocationID.x]++;
}

uint stack_handle_head(StackInfo self) {
    uint top_gives = gives_stack[self.stack_base + self.gives_nr - 1];
    if (is_nt(stack[top_gives - 1].sym)) {
        return top_gives - 1;
    }
    else {
        return top_gives;
    }
}

bool stack_handle_matches(StackInfo self, uint rule_offset, uint rule_length) {
    uint handle_head = stack_handle_head(self);
    if (stack_ptr[gl_GlobalInvocationID.x] - handle_head != rule_length) {
        return false;
    }
    for (uint i = 0; i < rule_length; i++) {
        if (stack[handle_head + i].sym != rules[rule_offset + i]) {
            return false;
        }
    }
    return true;
}

void stack_pop_handle(inout StackInfo self) {
    uint handle_head = stack_handle_head(self);
    stack_ptr[gl_GlobalInvocationID.x] = handle_head;
    self.gives_nr--;
    self.top_term = handle_head - 1;
}

uint stack_peek_top_term(StackInfo self) {
    return stack[self.top_term].sym;
}

const ParseError parse_error_no_error = ParseError(0, 0);

ParseError parse_error_at_location(uint location) {
    return ParseError(1, location);
}

void advance_head(inout uint head) {
    head++;
    error[gl_GlobalInvocationID.x] = parse_error_at_location(head - heads[gl_GlobalInvocationID.x]);  // This could change.
}

// Returns true iff the handle is reduced correctly.
bool stack_reduce_handle(inout StackInfo self) {
    uint offset = 0;
    uint rules_nr = rules[offset];
    offset++;
    for (uint i = 0; i < rules_nr; i++) {
        uint rule_lhs = rules[offset];
        offset++;
        uint rule_length = rules[offset];
        offset++;
        if (stack_handle_matches(self, offset, rule_length)) {
            stack_pop_handle(self);
            stack_push(self, StackSym(rule_lhs, PREC_UNDEF));
            return true;
        }
        else {
            offset += rule_length;
        }
    }
    return false;
}

bool repeat_condition(StackInfo self, uint head, uint end) {
    return head < end || (  // Short circuiting is guaranteed in GLSL for #version >= 440.
        head == end && !(
            stack_ptr[gl_GlobalInvocationID.x] - self.stack_base == 2
            && stack[self.stack_base].prec == PREC_UNDEF
            && is_nt(stack[self.stack_base + 1].sym)
            && stack[self.stack_base + 1].prec == PREC_UNDEF
        )
    );
}

uint get_prec(uint sym_i, uint sym_j) {
    return prec_mat[sym_i * term_thresh + sym_j];
}

void main() {
    uint thread_idx = gl_GlobalInvocationID.x;

    uint head = heads[thread_idx];
    error[thread_idx] = parse_error_at_location(head);
    StackInfo stack_info = stack_init();
    uint end = ends[thread_idx];
    while (repeat_condition(stack_info, head, end)) {
        if (is_nt(alpha[head])) {
            stack_push(stack_info, StackSym(alpha[head], PREC_UNDEF));
            advance_head(head);
        }
        else {
            uint top_term = stack_peek_top_term(stack_info);
            uint prec = get_prec(top_term, alpha[head]);
            switch (prec) {
                case PREC_GIVES:
                case PREC_EQUALS:
                    stack_push(stack_info, StackSym(alpha[head], prec));
                    advance_head(head);
                    break;
                case PREC_TAKES:
                    if (stack_info.gives_nr == 0) {
                        stack_push(stack_info, StackSym(alpha[head], prec));
                        advance_head(head);
                    }
                    else if (!stack_reduce_handle(stack_info)) {
                        return;  // Error
                    }
                    break;
                default:
                    return; // Error
            }
        }
    }
    error[thread_idx] = parse_error_no_error;
    if (stack_info.gives_nr == 0) {
        bot_gives[thread_idx] = 0;
    }
    else {
        bot_gives[thread_idx] = gives_stack[stack_info.stack_base] - stack_info.stack_base;
    }
}